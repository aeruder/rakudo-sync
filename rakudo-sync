#!/usr/bin/env raku

=begin pod
=head1 NAME

rakudo-sync - tool for automatically rsyncing when files change

=head1 DESCRIPTION

rakudo-sync uses fswatch and rsync to keep files in sync remotely.

=head1 CONFIGURATION

rakudo-sync loads the ~/.rakudo-sync.yml file at startup.  rakudo-sync.yml
should contain an array of from/to descriptions.  Each description can have
many properties:

Example:
  ---
  - name: myrepo
    from: /Users/aeruder/myrepo
    to: remote.host.com:/vol/aeruder/myrepo
    ignore-patterns:
      - /.git
      - /test-logs
  - name: configfiles
    from: /Users/aeruder/.dotfiles
    to: remote-host.com:.dotfiles

REQUIRED PROPERTIES:
  from: absolute path to directory
    to: rsync-style destination.  host:path/to/dir would be relative to
        the home directory remotely.  host:/path/to/dir is an absolute path
        remotely.

OPTIONAL PROPERTIES:
  name: a name for debug output, defaults to something based on the from and to
  sync-git: async(default)|no|yes
    async: sync .git directory independently from rest of files
    no:    skip syncing the .git directory
    yes:   sync .git directory with rest of files.  For large .git directories
           this can significantly slow down changes to non-git files.
  ignore-paths: an array of paths to ignore.  If the name starts with a '/'
                that file (relative to the 'from'/'to' paths) will
                not be copied (from 'from') or deleted (from 'to') during
                syncing.

                If the file does not begin with '/', that file will be ignored
                globally from syncing.

=end pod

use Config::TOML;

my $CONFIG-FILE = $*HOME.add(".config/rakudo-sync/config.toml");
my $PID-FILE = %*ENV<RAKUDO_SYNC_CTRL> ?? $*ENV<RAKUDO_SYNC_CTRL>.IO !! $*HOME.add(".config/rakudo-sync/ctrl.toml");

my @paths = load-config($CONFIG-FILE);

class FswatchRecursiveHandler {
    enum UpdateType<info fswatch-stderr state-change file-change rsync-stdout rsync-stderr rsync-finished heartbeat sync-check>;
    enum Status<idle standby syncing>;
    has Status:D $.status = idle;
    has IO::Path $.top;
    has Str $.to;
    has List:D $.ignore-patterns = List.new;
    has Supplier:D %!updates = UpdateType.enums.map({ $_.key => Supplier.new });
    has Supplier:D $!exit = Supplier.new;
    has Bool:D $!wants-exit = False;
    has Supplier:D $!changed-paths = Supplier.new;
    has Supplier:D $!synchronize = Supplier.new;
    has SetHash:D $!ignored-paths = SetHash.new;
    has Supplier:D $!holdoff-timer = Supplier.new;
    has DateTime $!holdoff-time = DateTime.now;
    has Bool:D $!entered-holdoff = False;
    has Bool:D $!needs-ignore-scan = False;
    has Status:D $!after-sync = idle;
    has Proc::Async $!rsync-process;
    has Supplier:D $!rsync-done = Supplier.new;
    constant fuzz-factor = 0.1;
    constant holdoff-delay = 1.0;

    method Supply(*@type) {
        Supply.merge(%!updates.{@type}.map({$_.Supply}));
    }

    method !schedule-holdoff {
        return if $!entered-holdoff;
        $!entered-holdoff = True;
        my $delta = $!holdoff-time - DateTime.now + fuzz-factor;
        $delta = fuzz-factor if $delta < fuzz-factor;
        $!holdoff-timer.emit: Supply.interval($delta).head(1);
    }

    method !emit-event(UpdateType:D $type, *@args) {
        %!updates{$type}.emit: (self, $type, @args).flat;
    }

    method !set-status(Status:D $status) {
        $!status = $status;
        self!emit-event(state-change, $status);
    }

    method !update-ignore(IO::Path:D $path) returns Bool:D {
        my $sep = $path.SPEC.dir-sep;
        my $top_abs = $!top.absolute;
        my $path_abs = $path.absolute;

        # somehow this path exists outside our directory-root, just ignore it
        if $path_abs ne $top_abs &&
          !$path_abs.starts-with($top_abs ~ $sep) {
            return True;
        }
        my Str:D $relpath = $path_abs eq $top_abs ?? "" !! $path_abs.substr($top_abs.chars + 1);

        my Bool:D $res = False;
        PATTERN: for @$!ignore-patterns -> $pattern {
            if ($pattern.substr(0,1) eq "/") {
                my $tmp_pattern = $pattern.substr(1);
                if $relpath ~~ /^ $tmp_pattern ($|$sep)/ {
                    $res = True;
                    last PATTERN;
                }
            } elsif $relpath ~~ /(^|$sep) $pattern ($|$sep)/ {
                $res = True;
                $!ignored-paths{"/" ~ $relpath} = 1 if $1 eq "";
                last PATTERN;
            }
        }

        return $res;
    }

    method !populate-ignore {
        return unless $!needs-ignore-scan;
        self!emit-event(info, "scanning directory for ignores");
        my @todo = $!top;

        PATH: while @todo {
            my $path = @todo.shift;
            next PATH if self!update-ignore($path);

            my ($link, $dir, $exists);
            {
                $exists = $path.e;
                $link = $exists && $path.l;
                $dir = !$link && $path.d;
                CATCH {
                    when X::IO::DoesNotExist {
                    }
                }
            };

            @todo.append($path.dir.list) if $dir;
        }

        self!emit-event(info, "finished scanning: " ~ $!ignored-paths.keys);
    }

    method !process-fswatch-line(Str:D $line) {
        my $path = $line.IO;
        return if self!update-ignore($path);
        $!changed-paths.emit: $path;
    }

    method gist returns Str {
        "FSwatchRecursiveHandler(top = " ~ $!top ~ ")";
    }

    method !sync-paths {
        return if $!rsync-process;
        $!after-sync = idle;
        self!set-status(syncing);
        my Str @cmd = qw|rsync -za --delete --verbose|;
        for $!ignored-paths.keys -> $ignored-path {
            @cmd.push("--exclude", $ignored-path);
        }
        @cmd.push("--", $!top.absolute ~ "/", $!to);
        my $rsync-process = $!rsync-process = Proc::Async.new(@cmd);
        self!emit-event(info, |@cmd);
        start {
            # sleep(5);
            react {
              whenever $rsync-process.stdout.lines -> $line {
                  self!emit-event(rsync-stdout, $line);
              }
              whenever $rsync-process.stderr.lines -> $line {
                  self!emit-event(rsync-stderr, $line);
              }
              whenever $rsync-process.start -> $res {
                  self!emit-event(rsync-finished, $res.exitcode);
                  $!rsync-done.emit: True;
                  done;
              }
            }
        }
    }

    method sync {
        my $supplier = Supplier.new;
        my $supply = $supplier.Supply;
        self!emit-event(sync-check, "started sync-check");
        react {
            whenever $supply {
                self!emit-event(sync-check, "done sync-check");
                $supplier.done;
                done;
            }
            $!synchronize.emit: $supplier;
        }
    }

    method run {
        my @fswatch_cmd = (qw|fswatch -l 0.5 -r|, $!top.path).flat;
        my $fswatch = Proc::Async.new(@fswatch_cmd);
        my @sync-done-supplier;
        react {
            whenever $fswatch.stdout.lines -> $line {
                self!process-fswatch-line($line);
            }
            whenever $fswatch.stderr.lines -> $line {
                self!emit-event(fswatch-stderr, $line);
            }
            whenever $fswatch.start {
                self!emit-event(info, "fswatch ended (" ~ $_.exitcode ~ "), exiting");
                if $!rsync-process {
                    self!emit-event(info, "rsync hanging around, killing rsync");
                    $!rsync-process.kill;
                    whenever Promise.in(2) {
                        $!rsync-process.kill: SIGKILL;
                    }
                    $!wants-exit = True;
                } else {
                    done;
                }
            }
            whenever $!synchronize -> $supplier {
                if $!status == idle {
                    self!sync-paths;
                }
                @sync-done-supplier.push: $supplier;
            }
            whenever $!exit {
                self!emit-event(info, "exit requested, killing fswatch");
                $fswatch.kill;
                whenever Promise.in(2) {
                    $fswatch.kill: SIGKILL;
                }
            }
            whenever $!changed-paths.Supply -> $path {
                self!emit-event(file-change, $path.absolute);
                if $!status == syncing {
                    $!after-sync = syncing;
                } else {
                    self!set-status(standby) if $!status == idle;
                    $!holdoff-time = DateTime.now + Duration.new(holdoff-delay);
                    self!schedule-holdoff;
                }
            }
            whenever $!holdoff-timer.Supply.migrate {
                $!entered-holdoff = False;
                my Duration:D $delta = $!holdoff-time - DateTime.now;
                if $delta.Real < 0 {
                    self!sync-paths;
                } else {
                    self!schedule-holdoff;
                }
            }
            whenever Supply.interval(60, 60) {
                self!emit-event(heartbeat);
            }
            whenever $!rsync-done.Supply {
                $!rsync-process = Nil;
                self!set-status(idle);
                if $!wants-exit {
                    for @sync-done-supplier -> $supplier {
                        $supplier.emit: True
                    }
                    @sync-done-supplier = @();
                    done;
                } elsif $!after-sync != idle {
                    self!sync-paths;
                } else {
                    for @sync-done-supplier -> $supplier {
                        $supplier.emit: True
                    }
                    @sync-done-supplier = @();
                }
            }
            self!populate-ignore;
            $!changed-paths.emit: $!top;
        }
        UpdateType.enums.map({ %!updates{$_.key}.done; });
    }

    method force-exit {
        $!exit.emit: True;
    }

    method TWEAK {
        $!top .= resolve;
        for @$!ignore-patterns -> $pattern {
            if ($pattern.substr(0,1) eq "/") {
                $!ignored-paths{$pattern} = 1;
            } else {
                $!needs-ignore-scan = True;
            }
        }
    }
}

my @verbosity = (
    FswatchRecursiveHandler::info,
    FswatchRecursiveHandler::fswatch-stderr,
    FswatchRecursiveHandler::state-change,
    FswatchRecursiveHandler::sync-check,
);
# my @verbosity = FswatchRecursiveHandler::UpdateType.enums.map({ $_.key });

my @info = @paths.map: {
    my $res = {};
    my $runner = FswatchRecursiveHandler.new(|($_<top ignore-patterns to>:p).Capture);
    my $name = $_<name>;
    $runner.Supply(@verbosity).act: -> $status { print-info($name, |$status.Capture) };
    my $thread = start { $runner.run };
    $res<runner name thread> = $runner, $name, $thread;
    $res;
};

signal(SIGINT).act: -> $sig {
    @info.race(batch => 1).map({ $_<runner>.force-exit });
};

my $socket = IO::Socket::Async.listen('127.0.0.1', 0);
react {
    my $socket-tap = do whenever IO::Socket::Async.listen('127.0.0.1', 0) -> $conn {
        start {
            react {
                whenever $conn.Supply.lines -> $line {
                    my $info = @info.first({ $_<name> eq $line});
                    if ($info) {
                        $info<runner>.sync;
                        $conn.print: "ðŸ‘\n";
                    } else {
                        $conn.print: "ðŸš«\n";
                    }
                    LAST {
                        done;
                    }
                    QUIT {
                        default {
                            say "Connection lost: $_";
                            done;
                        }
                    }
                }
            }
        }
    }
    for @info -> $info {
        whenever $info<thread> {
            @info.race(batch => 1).map({ $_<runner>.force-exit });
            done;
        }
    }
    whenever $socket-tap.socket-port -> $socket-port {
        my $record = %(
            listen-port => $socket-port,
            records => @(@info.map({ $_<name> })),
            pid => $*PID,
        );
        spurt $PID-FILE, to-toml($record);
    }
}

await Promise.allof(@info.map({ $_<thread> }));

sub print-info(Str:D $name, FswatchRecursiveHandler:D $obj, FswatchRecursiveHandler::UpdateType:D $type, *@args) {
    if @args {
        say "$name: $type - ", @args.join(" ");
    } else {
        say "$name: $type";
    }
}

sub load-config(IO::Path:D $path) returns Seq {
    my $toml = from-toml(file => $path.Str);
    my @paths = @($toml<paths>);
    my %output;
    my %combine_adds;

    for @paths.kv -> $idx, $path {
        my $early_name = $path<name> || "Record #{$idx}";
        die "Missing 'from' field in yaml file ($early_name)" unless $path<from>;
        die "Missing 'to' field in yaml file ($early_name)" unless $path<to>;
        $path<name> ||= ($path<from>, $path<to>).join(":");
        $path<from> .= IO;
        $path<to> ~~ s/\/$//;
        $path<top> = $path<from>:delete;
        $path<ignore-patterns> ||= [];
        $path<sync-git> ||= "async";

        given $path<sync-git> {
            when 'yes' {
            }
            when 'no' {
                $path<ignore-patterns>.push("/.git")
            }
            when 'async' {
                my $gitdir = $path<top>.add(".git");
                if $gitdir.e {
                    $path<ignore-patterns>.push("/.git");
                    my $new_name = $path<name> ~ ":git";
                    %combine_adds{$new_name} = {
                        name            => $new_name,
                        top             => $gitdir,
                        to              => $path<to> ~ "/.git",
                        ignore-patterns => [],
                    };
                }
            }
            default {
                die "Unknown sync-git value for {$path<name>}: {$path<sync-git>}";
            }
        }

        %output{$path<name>} = $path;
    }

    for %combine_adds.kv -> $key, $val {
        %output{$key} = Hash.new(|$val.pairs, |(%output{$key} || {}).pairs);
    }

    return %output.values;
}
